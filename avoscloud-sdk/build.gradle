apply plugin: 'com.android.library'


android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {  //this one may not be needed. But wont hurt.
        androidTest.setRoot('src/androidTest')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    task.exclude '**/BuildConfig.class'
    task.exclude '**/R.class'
    task.exclude '**/R$*.class'
    artifacts.add('archives', task);
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.alibaba:fastjson:1.2.37'
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.1.4"
    testCompile 'org.mockito:mockito-core:1.10.19'
}

apply from: '../maven_push.gradle'